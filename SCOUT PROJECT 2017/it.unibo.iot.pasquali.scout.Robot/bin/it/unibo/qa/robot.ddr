RobotSystem scout

// ------------------------------------------------------------------
//		EXPLORATION - INTERACTION
// ------------------------------------------------------------------

// From Console to Robot to start Exploration
Dispatch explore : explore
Dispatch exploredebug : exploredebug ( START , BOUNDS)
Dispatch explorefile : explorefile (START, FILENAME )
//START = position ( X , Y )
//BOUNDS = map ( W , H )
//TYPE = java || qa
//The second message is used for the debug

// From Robot to Console to notify a new cell state
Event expdata : expdata ( POS , STATE )
//POS = position ( X , Y )
//STATE = clear || object
//maybe i can send only the obstacle cell

// ------------------------------------------------------------------
//		NAVIGATION - INTERACTION
// ------------------------------------------------------------------

// From Console to Robot to start the Navigation
Dispatch navigate : navigate ( PLAN , POS )
//PLAN = plan ( [ t || d || l || r ] )
//POS = position ( Xs , Ys ) 
// 
//t = forward tiled
//d = forward diagonal
//l = left (45 or 90 degrees)
//r = right


// Raised by Robot when find an unexpected
// obstacle during the navigation
Event update : update ( OBJECT , CURRENT )
//ELEMENT = potition ( X , Y )
//CURRENT = position ( X , Y )


// ------------------------------------------------------------------
//		COMMON - INTERACTION
// ------------------------------------------------------------------

// Raised by Console to abort the current command
Event abort : abort

// Raised by Robot when it ends the current command
Event end : end


// ------------------------------------------------------------------
//		LOCAL - INTERACTION
// ------------------------------------------------------------------

// Raised by Sonar when an obstacle is detected
// The user can define the sensing distance in the sensorTheory.pl

//FrontSonar
Event obstaclefront : obstaclefront
Event obstacleleft : obstacleleft
// DIRECTION = front || left


// ------------------------------------------------------------------
//		ROBOT GUI - INTERACTION
// ------------------------------------------------------------------

Dispatch enableGUI : enableGUI(STATE)
// STATE = on || off

Event show : show(POS, DIR)
// POS = position(X,Y)
// DIR = n || e || w || s || ne || nw || se || sw


// ------------------------------------------------------------------
//			CONTEXTS
// ------------------------------------------------------------------

Context ctxRobot ip [ host="localhost" port=8020]
Context ctxConsole ip [ host="localhost" port=8010]

QActor console context ctxConsole{
 	Plan init normal
		println( "NEVER HERE. I'm just a placeholder" ) 
 }

QActor guimanager context ctxRobot{
	
	Plan init normal
		println("++++++++++++++++++++++++++ guiManager(starts) ++++++++++++++++++");
		solve consult("guiTheory.pl") time(210000000) onFailSwitchTo  consultPrologFailure ;
		switchToPlan waitEnableMessage
		
	Plan waitEnableMessage
	
		receiveMsg time(100000000);
		onMsg enableGUI : enableGUI(on) -> solve showGUI time(0);
		onMsg enableGUI : enableGUI(off) -> endQActor "GUI disabled";		
		switchToPlan waitMap
	
	Plan waitMap		
		receiveMsg time(1000000000);
		onMsg explorefile : explorefile(START, FILENAME) -> solve showMap(START, FILENAME) time(0);
		
		switchToPlan waitUpdates	
		
	Plan waitUpdates
		sense time(1000000000) show, end, abort -> continue, continue, continue;
		
		onEvent show : show(POS,DIR) -> solve updateState(POS,DIR) time(0);
		onEvent end : end -> resumeLastPlan;
		onEvent abort : abort -> resumeLastPlan;
		
		repeatPlan 0 
		
		
		
	Plan consultPrologFailure            
		println("GUI ----------------------------- consult gone wrong" )
}

Robot mockScout QActor robot context ctxRobot{

// ------------------------------------------------------------------
//			NORMAL PLANS
// ------------------------------------------------------------------

	
	Plan init normal
		
		println("load theories");
		
		//solve consult("robotTheory.pl") time(210000000) onFailSwitchTo  consultPrologFailure ;
		//solve consult("navRobotTheory.pl") time(210000000) onFailSwitchTo  consultPrologFailure ;
		solve consult("exploreRobotTheory.pl") time(210000000) onFailSwitchTo  consultPrologFailure ;
		//solve consult("sensorTheory.pl") time(210000000) onFailSwitchTo  consultPrologFailure ;
		//solve consult("talkTheory.pl") time(210000000) onFailSwitchTo  consultPrologFailure ;
		
 		println("++++++++++++++++++ robot(starts) ++++++++++++++++++");
 		
 		[!? robotMode(gui)] forward guimanager -m enableGUI : enableGUI(on) else
 						 forward guimanager -m enableGUI : enableGUI(off);
 		
 		switchToPlan waitConsoleCommand
 		
 	Plan waitConsoleCommand
 		
		println("++++++++++++++++++ WAIT CONSOLE COMMAND ++++++++++++++++++");
 		
 		receiveMsg time(600000000);
 		printCurrentMessage;
 		memoCurrentMessage;
 		
 		onMsg explore : explore -> switchToPlan exploration;
 		onMsg exploredebug : exploredebug ( START , BOUNDS) -> switchToPlan explorationDebug;
 		onMsg explorefile : explorefile (FILENAME) -> switchToPlan exploration;
 		onMsg navigate : navigate(PLAN, POS) -> switchToPlan navigation;
 		
 		repeatPlan 0		
 
 // EXPLORATION PLANS----------------------------------------------
 		
 	Plan exploration
 		println("")
 
 // DEBUG EXPLORATION PLANS -----------------------------------		
 	
 	Plan fileexplorer
 		println("Let's explore by java!!"); 		
 		[?? msg(_, _, SENDER, X, explorefile(START, FILENAME), MSGNUM)] solve initExploreMap(START, FILENAME) time(0)
 	
 	
 	 Plan explorationDebug
 		println("Let's explore!!");
 		[?? msg(_, _, SENDER, X, explore(START,BOUNDS), MSGNUM)] solve 	initExploreMap(START, BOUNDS) time(0);
 		
 		switchToPlan notifyCell;
 		
 		solve assert(explore(true)) time(0);
 		
 		switchToPlan findLeftWall
 		
 		
 	Plan findLeftWall
 		println("Find Left Wall!");
 		 
 		sense time(1000) obstaclefront -> continue;
 		
 		// OBSTACLE FRONT
 		
 		onEvent obstaclefront : obstaclefront -> solve assert(obstacle(front, obstacle)) time(0) else
 										solve assert(obstacle(front, clear)) time(0);
 										
 		[!? obstacle(front, clear)] solve updateModel(front, clear) time(0);
		[?? obstacle(front, clear)] switchToPlan notifyCell;
		// [?? obstacle(front, clear)] continue	
 		
 		[!? obstacle(front, obstacle)] solve updateModel(front, obstacle) time(0);
 		[!? obstacle(front, obstacle)] switchToPlan notifyCell;								
 		[?? obstacle(front, obstacle)] switchToPlan turnRightAndFollow; 
 		 		
 				
 		// ----------------------------------------------------------------------------------
 		
 		sense time(1000) obstacleleft -> continue;
 		
 		// OBSTACLE LEFT
 		
 		onEvent obstacleleft : obstacleleft -> solve assert(obstacle(left, obstacle)) time(0) else
 										solve assert(obstacle(left, clear)) time(0);
 										
 		[!? obstacle(left, clear)] solve updateModel(left, clear) time(0);
		[?? obstacle(left, clear)] switchToPlan notifyCell;
		// [?? obstacle(left, clear)] continue			
 		
 		[!? obstacle(left, obstacle)] solve updateModel(left, obstacle) time(0);
 		[!? obstacle(left, obstacle)] switchToPlan notifyCell;								
 		[?? obstacle(left, obstacle)] switchToPlan followWall;
 		
 		// -----------------------------------------------------------------------------------
 		
 		println("Forward Step");	
 		
 		solve moveForward time (10000000);
 		solve addCurrentToVisited time (10000000);
 		
 		repeatPlan 0
 		
 		
 		
 	Plan turnRightAndFollow
 		println("turn right and follow"); 		
 		solve turnDoubleRight time(0);
 		switchToPlan followWall
 		
 	Plan turnLeftAndFind
 		println("Wall Found!! - Left"); 		
 		solve turnDoubleLeft time(0);
 		switchToPlan findLeftWall
 		
 	Plan turnLeft resumeLastPlan
 		println("turn Left"); 		
 		solve turnDoubleLeft time(0)
 		
 	Plan turnRight resumeLastPlan
 		println("turn Right"); 		
 		solve turnDoubleLeft time(0)

 		
 	Plan followWall
 		println("Follow Wall");
 		
 		sense time(1000) obstacleleft -> continue;
 		
 		// OBSTACLE LEFT
 		
 		onEvent obstacleleft : obstacleleft -> solve assert(obstacle(left, object)) time(0) else
 										solve assert(obstacle(left, clear)) time(0);
 										
 		[!? obstacle(left, clear)] solve updateModel(left, clear) time(0);
		[?? obstacle(left, clear)] switchToPlan notifyCell;		
 		
 		[!? obstacle(left, object)] solve updateModel(left, object) time(0);
 		[!? obstacle(left, object)] switchToPlan notifyCell;								
 		[?? obstacle(left, object)] switchToPlan turnLeftAndFind;									
 		 										
 					
 		sense time(1000) obstaclefront -> continue;
 		
 		// OBSTACLE FRONT
 		
 		onEvent obstaclefront : obstaclefront -> solve assert(obstacle(front, object)) time(0) else
 										solve assert(obstacle(front, clear)) time(0);
 										
 		[!? obstacle(front, clear)] solve updateModel(front, clear) time(0);
		[!? obstacle(front, clear)] switchToPlan notifyCell;									
 		[?? obstacle(front, clear)] switchToPlan moveAndCheckLoop;		
 		
 		[!? obstacle(front, object)] solve updateModel(front, object) time(0);
 		[!? obstacle(front, object)] switchToPlan notifyCell;								
 		[?? obstacle(front, object)] switchToPlan frontWallFound;
 		
 		repeatPlan 0 		
 		 		
 		 		
 		 		
 		 		
 		 		
 	Plan frontWallFound
 		println("I hit a wall"); 		
 		
 		// OBSTACLE LEFT
 		
 		sense time(1000) obstacleleft -> continue;
 		
 		onEvent obstacleleft : obstacleleft -> solve assert(obstacle(left, object)) time(0) else
 										solve assert(obstacle(left, clear)) time(0);
 										
 		[!? obstacle(left, clear)] solve updateModel(left, clear) time(0);
		[!? obstacle(left, clear)] switchToPlan notifyCell;		
 		
 		[!? obstacle(left, object)] solve updateModel(left, object) time(0);
 		[!? obstacle(left, object)] switchToPlan notifyCell;
 		
 		solve senseAndCheckLeft time (1000000);
 		
 		[!? notLeftObjectAndNotExplored(true)] switchToPlan turnLeft
 		 else switchToPlan turnRight;
 		 
 		resumeLastPlan // return to followWall
 		 
 	Plan moveAndCheckLoop
 		println("let's move");
 		
 		solve moveForward time(100000);
 		
 		solve checkCurrentAlreadyVisited time(100000);
 		
 		[!? currentAlreadyVisited(false)] switchToPlan loopAvoidance
 		 else solve addCurrentToVisited time (10000000);
 		
 		resumeLastPlan
 		
 	 	
 	
 	Plan loopAvoidance	
 		println("I'm in a looooop");
 		
 		solve findNearestNotExploredCell time(1000000);
 		
 		[!? explore(false)] switchToPlan endOfExploration;
 		
 		solve computeBestPath time(1000000000);
 		
 		solve travel time(1000000000);
 		
 		switchToPlan findLeftWall
 		
 	
 	Plan notifyCell resumeLastPlan
 		println("notify clear cell");
 		[?? newCell(POS, STATE) ] emit expdata : expdata( POS , STATE )
 		
 		
 	Plan endOfExploration
 		println("Exploration done")	
 
 // *********** NAVIGATION PLANS **********************
 		
 	Plan navigation
 		println("I Received some navigation data");
 		[?? msg(_, _, SENDER, X, navigate(PLAN, POS), MSGNUM)] 
 			solve loadNavigationData(PLAN, POS) time (100000) onFailSwitchTo loadMapFailure;
 		
 		switchToPlan startNavigation 	
 		
 		
 	Plan startNavigation
 	
 		[ !? planFilename(FILENAME) ]  solve loadThePlan(FILENAME) time(300000000);
  		
 		println("++++++++++++++++++ Plan Loaded ++++++++++++++++++");	
 		println(" ");
 		println(" ");
 		
 		[ !? planName(PLANNAME) ] solve showPlan(PLANNAME) time(1000);
 		
 		println(" ");
 		println(" ");
 		println("++++++++++++++++++ ++++++++++++++++++ ++++++++++++++++++");	

		[ !? planName(PLANNAME) ] solve myRunPlan(PLANNAME) time(1000000000)
		// i's a big atto di fede 
 		//solve runResumablePlan(path) time(2100000000)
 	
 	Plan abort 
 		println("Current command aborted");
 		switchToPlan waitConsoleCommand
 		
 	Plan waitAndEvaluate resumeLastPlan
 		
 		println("maybe there is an unexpected static obstacle");
 		delay time(60000);
 		println("are you there obstacle ??");
 		
 		sense time(5000) obstaclefront -> notifyUnexpectedObstacle	
 		
 		
 	Plan notifyUnexpectedObstacle
 		
 		println("I found an unexpected static obstacle");
 		
 		solve getCurrentPosition(CURRENT) time(2100000000) onFailSwitchTo navigationFailure;
 		solve getObstaclePosition(OBJECT) time(2100000000) onFailSwitchTo navigationFailure;
 	
 		emit update : update(OBJECT , CURRENT);
 		
 		println("Unexpected Obstacle notified, I wait the new plan");
 		
 		switchToPlan waitConsoleCommand 		
 		
 	
 	Plan notifyEndOfNavigation	
 	
 		println("passato al piano successivo"); 	
 			
 		solve notifyEnd time(10000) onFailSwitchTo generalPrologFailure;	
 		
 		println("ma la chiamata prolog fallisce per un motivo non ben definito")
		//TODO	
	
// ------------------------------------------------------------------
//	PROLOG FAILURE ERROR HANDLERS
// ------------------------------------------------------------------
		
	Plan handleTimeout
 		println("timeout!! GOODBYE")
 	
 	Plan explorationFailure            
		println("Explore FAILURE" ) 
 	
 	Plan loadMapFailure           
		println("Load Map FAILURE" ) 
		 
 	Plan findpathFailure           
		println("Find Path FAILURE" )  
 	
 	Plan navigationFailure            
		println("Navigation FAILURE" )
		
	Plan alternativeFindpathFailure            
		println("Alternative Find Path FAILURE" )  
 		
 	Plan consultPrologFailure            
		println("The consult is gone wrong, maybe there are errors in the prolog file" ) 
 		
 	Plan generalPrologFailure            
		println("Prolog goal FAILURE" ) 
}









