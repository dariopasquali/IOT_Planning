System scout

// ------------------------------------------------------------------
//		EXPLORATION - INTERACTION
// ------------------------------------------------------------------

// From Console to Robot to start Exploration
Dispatch explore : explore
Dispatch exploredebug : exploredebug( START , BOUNDS )
Dispatch explorefile : explorefile(START , FILENAME )
//START = position ( X , Y )
//BOUNDS = map ( W , H )

// From Robot to Console to notify a new cell state
Event expdata : expdata( POS , STATE )
//POS = position ( X , Y )
//STATE = clear || object

// ------------------------------------------------------------------
//		NAVIGATION - INTERACTION
// ------------------------------------------------------------------

// From Console to Robot to start the Navigation
Dispatch navigate : navigate( PLAN_MOVES , POS )
Dispatch navigatefile : navigatefile( PLAN_MOVES , POS , FILENAME)
//PLAN_MOVES = plan ( [ t || d || l || r ] )
//POS = position ( Xs , Ys )
// 
//t = forward tiled
//d = forward diagonal
//l = left (45 or 90 degrees)
//r = right


// Raised by Robot when find an unexpected
// obstacle during the navigation
Event update : update( OBJECT , CURRENT )
//OBJECT = potition ( X , Y )
//CURRENT = position ( X , Y )


// ------------------------------------------------------------------
//		COMMON - INTERACTION
// ------------------------------------------------------------------

// Raised by Console to abort the current command
Event abort : abort

// Raised by Robot when it ends the current command
Event end : end


// ------------------------------------------------------------------
//		LOCAL - INTERACTION
// ------------------------------------------------------------------


// Raised by the GUI on user interaction
Event local_gui_command : local_gui_command(COMMAND)
 
//Button --> COMMAND
//
//Explore --> explore || explore(FILENAME, START, BOUNDS, MODE); MODE = simulated || robot
//Load Map --> loadmap ( PATH );
//Find Path â€“-> findpath ( START , GOAL );
//Start Navigation --> navigate(MODE); MODE = simulated || robot
//Abort --> abort;
//Clear Path --> clearpath
//Clear Exploration --> clear


// ------------------------------------------------------------------
//			CONTEXTS
// ------------------------------------------------------------------

Context ctxRobot ip [ host="localhost" port=8020]
Context ctxConsole ip [ host="localhost" port=8010]

QActor console context ctxConsole{
	
// ------------------------------------------------------------------
//			NORMAL PLANS
// ------------------------------------------------------------------
	
	Plan init normal
		solve consult("expConsoleTheory.pl") time(0) onFailSwitchTo  consultPrologFailure ;
		solve consult("navConsoleTheory.pl") time(0) onFailSwitchTo  consultPrologFailure ;		
		solve consult("consoleTheory.pl") time(0) onFailSwitchTo  consultPrologFailure ;
		
 		println("++++++++++++++++++ console(starts) ++++++++++++++++++");
 		switchToPlan waitGUICommand
 		
 		
 	Plan waitGUICommand
		println("++++++++++++++++++ WAIT COMMAND ++++++++++++++++++");
		
		sense time(999999999) local_gui_command -> continue;
				
		printCurrentEvent;
		memoCurrentEvent;
		
		onEvent local_gui_command : local_gui_command (explore) -> switchToPlan exploration;
		
 		onEvent local_gui_command : local_gui_command (explore(FILENAME, START, BOUNDS, MODE)) ->
 																		 switchToPlan explorationDebug;
 																		 
 		onEvent local_gui_command : local_gui_command (loadmap(PATH)) -> switchToPlan loadMap;
 		onEvent local_gui_command : local_gui_command (findpath(START,GOAL)) -> switchToPlan findPath;
 		onEvent local_gui_command : local_gui_command (navigate(robot)) -> switchToPlan navigation;
 		onEvent local_gui_command : local_gui_command (navigate(simulated)) -> switchToPlan navigationFile;
 		onEvent local_gui_command : local_gui_command (clear) -> switchToPlan clearGUI;
 		onEvent local_gui_command : local_gui_command (clearpath) -> switchToPlan clearPath;
		
		repeatPlan 0
		
	Plan loadMap resumeLastPlan
		[?? msg(local_gui_command, "event", SENDER, none, local_gui_command (loadmap(PATH)), MSGNUM)]
			 actorOp loadMapButton(PATH);
				
		println("++++++++++++++++++ MAP LOADED ++++++++++++++++++")
	
// EXPLORATION ----------------------------------------------------------------------------
	
	Plan exploration
		println("++++++++++++++++++ EXPLORATION ++++++++++++++++++");
		
		solve showClearMap time(60000) onFailSwitchTo  explorationFailure;	
		forward robot -m explore : explore;
	
		switchToPlan waitEndOfExploration
			
	Plan explorationDebug
		println("++++++++++++++++++ EXPLORATION DEBUG ++++++++++++++++++");
					
		[!? msg(local_gui_command, "event", SENDER, none, local_gui_command (explore(FILENAME,START,map(W,H),MODE)), MSGNUM)]
			actorOp showClearMap(W,H);	
		
		println("mappa pulita");
			
		[!? msg(local_gui_command, "event", SENDER, none, local_gui_command (explore(FILENAME,START,BOUNDS, debug)), MSGNUM)]
			 forward robot -m exploredebug : exploredebug(START,BOUNDS);		
		
		[?? msg(local_gui_command, "event", SENDER, none, local_gui_command (explore(FILENAME,START,BOUNDS, simulated)), MSGNUM)]
			 forward robot -m explorefile : explorefile(START,FILENAME);		 
				
		switchToPlan waitEndOfExploration

	
			
	Plan waitEndOfExploration resumeLastPlan
		sense time(999999999) local_gui_command, expdata, end -> continue, continue, continue;
		
		printCurrentEvent;
		
		onEvent expdata : expdata(position( X , Y ) , STATE) ->	actorOp updateMap( X , Y , STATE);
			
		onEvent local_gui_command : local_gui_command(abort) ->	switchToPlan abortCommand;
		
		onEvent end : end -> switchToPlan endOfExploration;			
			
		repeatPlan 0
			
			
	Plan endOfExploration
		println("Exploration ENDED");
		println("Please Save the map, or clear and repeat");
		
		switchToPlan waitGUICommand
			
// --------------------- NAVIGATION --------------------------------

			
	Plan findPath resumeLastPlan
	
		[?? msg(local_gui_command, "event", SENDER, none, local_gui_command (findpath(START,GOAL)), MSGNUM)]
			solve searchBestPath(START,GOAL) time(210000000) onFailSwitchTo  findpathFailure;
	
		println("++++++++++++++++++ PATH FOUND ++++++++++++++++++")
	
			
	Plan navigation
		println("++++++++++++++++++ NAVIGATION ++++++++++++++++++");
		
		actorOp sendNavigationData("robot");		
		switchToPlan waitEndOfNavigation
		
		
	Plan navigationFile
		println("++++++++++++++++++ NAVIGATION FILE ++++++++++++++++++");
		
		actorOp sendNavigationData("simulated");		
		switchToPlan waitEndOfNavigation	
		
		
	Plan waitEndOfNavigation
		sense time(999999999) local_gui_command, update, end -> continue, continue, continue;
 		
 		printCurrentEvent;
 		memoCurrentEvent;
 		
 		onEvent local_gui_command : local_gui_command(abort) -> switchToPlan abortCommand; 
 						
		onEvent update : update (OBJECT, CURRENT) ->
			 solve updateMapAndReplan(OBJECT , CURRENT) time(0)
			 onFailSwitchTo  alternativeFindpathFailure;
		
		onEvent end : end -> switchToPlan endOfNavigation;
		
		repeatPlan 0

	Plan endOfNavigation
	
		println("++++++++++++++++++ NAVIGATION ENDED :D ++++++++++++++++++");
		switchToPlan waitGUICommand	
	
// COMMON -----------------------------------------------------------------------------	
	
	Plan abortCommand
		emit abort : abort;
		println("++++++++++++++++++ COMMAND ABORTED ++++++++++++++++++");
		switchToPlan waitGUICommand	
		
	Plan clearGUI resumeLastPlan
		actorOp myClearGUI;
		println("map cleared")
	
	Plan clearPath resumeLastPlan
		solve clearPath time(10000);
		println("path cleared")

// ------------------------------------------------------------------
//	PROLOG FAILURE ERROR HANDLERS
// ------------------------------------------------------------------
		
	Plan handleTimeout
 		println("timeout!! GOODBYE")
 	
 	Plan explorationFailure            
		println("Explore FAILURE" ) 
 	
 	Plan loadMapFailure           
		println("Load Map FAILURE" ) 
		 
 	Plan findpathFailure           
		println("Find Path FAILURE" )  
 	
 	Plan navigationFailure            
		println("Navigation FAILURE" )
		
	Plan alternativeFindpathFailure            
		println("Alternative Find Path FAILURE" )  
 		
 	Plan consultPrologFailure            
		println("The consult is gone wrong, maybe there are errors in the prolog file" ) 
 		
 	Plan generalPrologFailure            
		println("Prolog goal FAILURE" ) 	
	
}


QActor robot context ctxRobot{
 	Plan init normal		
		println( "just a placeholder" ) 	
 }
